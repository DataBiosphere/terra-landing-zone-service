plugins {
    id 'bio.terra.landingzone.java-spring-library-conventions'
    id 'de.undercouch.download'
    id 'com.google.cloud.tools.jib'
    id 'com.srcclr.gradle'
    id 'org.sonarqube'
    id 'com.jfrog.artifactory' version '4.18.2'
    id 'org.liquibase.gradle' version '2.1.0'
}

apply from: 'publishing.gradle'

dependencies {
    implementation 'bio.terra:terra-common-lib'
    implementation 'org.apache.commons:commons-dbcp2:2.9.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc:3.0.5'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.7.0'
    implementation 'org.springframework.retry:spring-retry:1.3.3'
    implementation 'org.broadinstitute.dsde.workbench:sam-client_2.13:0.1-6d19a41'
    implementation 'bio.terra:billing-profile-manager-client:0.1.29-SNAPSHOT'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'org.postgresql:postgresql:42.5.1'
    implementation 'javax.servlet:jstl:1.2'
    implementation group: 'com.azure', name: 'azure-core', version: '1.34.0'
    implementation group: 'com.azure', name: 'azure-identity', version: '1.7.1'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-compute', version: '2.18.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-containerinstance', version: '2.25.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-msi', version: '2.18.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-relay', version: '1.0.0-beta.1'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-resources', version: '2.25.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-batch', version: '1.0.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-postgresql', version: '1.0.2'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-loganalytics', version: '1.0.0-beta.3'

    // at commit time, azure-resourcemanager-monitor v2.20.0 is the latest but starting with v2.19.0
    // attributes of com.azure.resourcemanager.monitor.fluent.models.DataCollectionRuleResourceInner
    // are missing making creation of DataCollectionRules impossible via java client lib
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-monitor', version: '2.25.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-applicationinsights', version: '1.0.0-beta.5'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager', version: '2.18.0'

    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.0'
    liquibaseRuntime 'info.picocli:picocli:4.7.2'
    liquibaseRuntime 'org.postgresql:postgresql:42.5.0'
    liquibaseRuntime 'ch.qos.logback:logback-classic:1.2.10'

    // Spotbugs dependencies
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
    spotbugs "com.github.spotbugs:spotbugs:${spotbugs.toolVersion.get()}"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'

    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation('org.springframework.boot:spring-boot-starter-test:3.0.5') {
        // Fixes warning about multiple occurrences of JSONObject on the classpath
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
}

// We use Spring Boot gradle plugin in a non-Spring Boot environment to manage Spring Boot dependency.
// Disable bootJar and enable jar task back since this is actually not a Spring Boot app.
bootJar {
    enabled = false
}

jar {
    enabled = true
}

// Modify the standard :test task to only include unit-tagged and library-tagged tests.
test {
    useJUnitPlatform {
        includeTags "unit", "library"
    }
}

task unitTest(type: Test) {
    useJUnitPlatform {
        includeTags "unit"
    }
    outputs.upToDateWhen { false }
}

// Create a new :integrationTest task to run integration tests. This won't be executed as
// part of the standard `./gradlew build` invocation.
task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
    // Force tests to always be re-run, since integration tests involve communicating with external
    // resources.
    outputs.upToDateWhen { false }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'terra-landing-zone-service'
        property 'sonar.projectKey', 'DataBiosphere_terra-landing-zone-service'
        property 'sonar.organization', 'broad-databiosphere'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

liquibase {
    activities {
        catalog {
            changeLogFile 'src/main/resources/db/changelog.xml'
            url 'jdbc:postgresql://localhost:5432/landingzone_db'
            username 'dbuser'
            password 'dbpwd'
            logLevel 'info'
        }
    }
}
