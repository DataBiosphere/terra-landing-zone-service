plugins {
    id 'idea'
    id 'jacoco'
    id 'java'

    id 'com.diffplug.spotless'
    id 'org.hidetake.swagger.generator'
    id 'com.srcclr.gradle'
}

boolean isGithubAction = System.getenv().containsKey("GITHUB_ACTIONS")

if (!isGithubAction) {
    tasks.withType(JavaExec).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
    tasks.withType(Test).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    maven {
        // Terra proxy for maven central
        url 'https://broadinstitute.jfrog.io/broadinstitute/maven-central/'
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    compileOnly "com.google.code.findbugs:annotations:3.0.1"
    implementation 'io.swagger.core.v3:swagger-annotations:2.1.12'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:2.2.0'
    // Needed for @VisibleForTesting and more:
    // TCL 1.0.10 tries to expose this as an api dependency, but does so without specifying an explicit version
    // (it derives the version from com.google.cloud:libraries-bom, which it doesn't expose to consumers).
    // If TCL instead exposed the BOM, or exposed guava with a specified version, this could be removed.
    implementation 'com.google.guava:guava:33.1.0-jre'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.47'

    implementation ('bio.terra:terra-common-lib:1.1.6-SNAPSHOT') {
        exclude group: "org.broadinstitute.dsde.workbench", module: "sam-client_2.13"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

version = gradle.releaseVersion
group = 'bio.terra'

// for scans
if (hasProperty("buildScan")) {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}

spotless {
    java {
        targetExclude "${buildDir}/**"
        targetExclude "**/swagger-code/**"
        googleJavaFormat()
    }
}

// Run spotless check when running in github actions, otherwise run spotless apply.
compileJava {
    if (isGithubAction) {
        dependsOn(spotlessCheck)
    } else {
        dependsOn(spotlessApply)
    }
}

srcclr {
    scope = "runtimeClasspath"
}

jacocoTestReport {
    reports {
        // sonarqube requires XML coverage output to upload coverage data
        xml.required = true
    }
}
