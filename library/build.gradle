plugins {
    id 'bio.terra.landingzone.java-spring-library-conventions'
    id 'de.undercouch.download'
    id 'com.google.cloud.tools.jib'
    id 'com.srcclr.gradle'
    id 'org.sonarqube'
    id 'com.jfrog.artifactory'
}

apply from: 'publishing.gradle'

dependencies {
    implementation 'org.apache.commons:commons-dbcp2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework:spring-aop'
    implementation 'org.springframework:spring-aspects'
    //we need to expose it at service module for status checking capability
    api 'org.broadinstitute.dsde.workbench:sam-client_2.13:0.1-6d19a41'
    implementation 'bio.terra:billing-profile-manager-client:0.1.484-SNAPSHOT'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api'
    implementation group: 'com.azure', name: 'azure-core', version: '1.51.0'
    implementation (group: 'com.azure', name: 'azure-identity', version: '1.13.2') {
        // azure-identity is still pulling in an old version of reactor-netty-http
        exclude group: 'io.projectreactor.netty', module: 'reactor-netty-http'
        exclude group: 'io.projectreactor.netty', module: 'reactor-netty-core'
        exclude group: 'io.projectreactor', module: 'reactor-core'
    }
    implementation group: 'io.projectreactor.netty', name: 'reactor-netty-http', version: '1.1.21'
    implementation group: 'io.projectreactor.netty', name: 'reactor-netty-core', version: '1.1.21'
    implementation group: 'io.projectreactor', name: 'reactor-core', version: '3.6.8'

    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager', version: '2.41.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-relay', version: '1.0.0-beta.2'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-batch', version: '1.0.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-postgresqlflexibleserver', version: '1.0.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-loganalytics', version: '1.0.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-applicationinsights', version: '1.0.0'
    implementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-securityinsights', version: '1.0.0-beta.4'

    implementation group: "io.kubernetes", name: "client-java", version: "20.0.1" // Do not use -legacy versions
    constraints {
        implementation('org.bouncycastle:bcprov-jdk18on:1.78.1') {
            because 'https://broadworkbench.atlassian.net/browse/WOR-1650'
        }
    }
    implementation group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: '1.13.2'

    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// We use Spring Boot gradle plugin in a non-Spring Boot environment to manage Spring Boot dependency.
// Disable bootJar and enable jar task back since this is actually not a Spring Boot app.
bootJar {
    enabled = false
}

jar {
    enabled = true
}

// Modify the standard :test task to only include unit-tagged and library-tagged tests.
test {
    useJUnitPlatform {
        includeTags "unit", "library"
    }
}

task unitTest(type: Test) {
    useJUnitPlatform {
        includeTags "unit"
    }
    outputs.upToDateWhen { false }
}

// Create a new :integrationTest task to run integration tests. This won't be executed as
// part of the standard `./gradlew build` invocation.
task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
    // Force tests to always be re-run, since integration tests involve communicating with external
    // resources.
    outputs.upToDateWhen { false }

    testLogging {
        events = ["passed", "failed", "skipped", "started", "standard_out", "standard_error"]
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'terra-landing-zone-service'
        property 'sonar.projectKey', 'DataBiosphere_terra-landing-zone-service'
        property 'sonar.organization', 'broad-databiosphere'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}
